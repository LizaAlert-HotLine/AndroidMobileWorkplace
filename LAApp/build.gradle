apply plugin: 'com.android.application'
apply plugin: "jp.leafytree.android-scala"


dependencies {
//    compile fileTree(dir: 'libs', include: '*.jar')
    compile project(':CoreLib')
    compile 'com.android.support:appcompat-v7:21.0.2'
    compile group: 'com.android.support', name: 'support-v13', version: '21.0.2'
    compile 'com.yandex.android:mobmetricalib:1.60'
    compile 'com.google.protobuf:protobuf-java:2.5.0'

    compile "com.android.support:multidex:1.0.0"
    compile "org.scala-lang:scala-library:2.11.5"

    // to break 64k limit
    compile "org.scalaz:scalaz-core_2.11:7.1.0"
    compile "com.google.guava:guava-jdk5:17.0"
    compile "org.apache.commons:commons-math3:3.3"
}

task copyFilesRelease(type: Copy) {
    description = 'Copies assets files...'
    from '../LICENSE'
    from '../AUTHORS'
    into "build/intermediates/assets/release"
}
task copyFilesDebug(type: Copy) {
    description = 'Copies assets files...'
    from '../LICENSE'
    from '../AUTHORS'
    into "build/intermediates/assets/debug"
}

android {
    compileSdkVersion "android-21"
    buildToolsVersion "21.1.2"

    lintOptions {
        abortOnError true
        disable 'InvalidPackage', 'AppCompatMethod', 'UnusedResources', 'IconMissingDensityFolder', 'AppCompatResource'
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        applicationId "ru.lizaalert.common"
        testApplicationId "ru.lizaalert.hotline.test"
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
//        versionCode 1
//        versionName "1.0"
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "2g"
    }

    signingConfigs {
        release {
            if(System.getenv("KEYSTORE")) {
                storeFile new File(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }

        }
    }

    buildTypes {
        debug {
//            applicationIdSuffix ".debug"

            minifyEnabled true 
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-project.txt'


        }
        release {
            if(System.getenv("KEYSTORE")) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

tasks.whenTaskAdded { task ->
//	print 'added '
//	println task.name
	if (task.name == 'generateDebugResources') {
		task.dependsOn copyFilesDebug
	}
	if (task.name == 'generateReleaseResources') {
		task.dependsOn copyFilesRelease
	}
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith("dex")
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += "--multi-dex"
        dx.additionalParameters += "--main-dex-list=$projectDir/main-dex-list.txt".toString()
    }
}
